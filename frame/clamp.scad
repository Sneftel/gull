include <params.scad>
include <util.scad>

BEND_RADIUS = 80;

FINGERBOARD_PCB_LENGTH = FINGERBOARD_FORWARD_EXTENT + FINGERBOARD_REARWARD_EXTENT;
FINGERBOARD_PCB_ANGLE = angleFromLen(BEND_RADIUS, FINGERBOARD_PCB_LENGTH);

PCB_SUPPORT_HEIGHT = 6;
PCB_SUPPORT_MID_HEIGHT = 2;
PCB_SUPPORT_MID_WIDTH = 3;
PCB_SUPPORT_BOTTOM_WIDTH = 8;

BODY_RADIUS = BEND_RADIUS - PCB_SUPPORT_HEIGHT;

BODY_THICKNESS = 10;

ENDHOOK_WIDTH = 5;
ENDHOOK_OVERHANG = 6;
ENDHOOK_CLEARANCE = 3;
ENDHOOK_PAD_WIDTH = 3;

UNDERCUT_WIDTH = 16.5;
UNDERCUT_DEPTH = 5;

module OnArc(x)
{
    RotZ(-angleFromLen(BEND_RADIUS, x)) translate([0,BEND_RADIUS])
        children();
}

module PCBSupport()
{
	ROUNDING_HEIGHT = PCB_SUPPORT_HEIGHT-PCB_SUPPORT_MID_WIDTH/2;
	EXTRA=1;
	pn_pos() {
		translate([0,-PCB_SUPPORT_MID_WIDTH/2]) {
			circle(d=PCB_SUPPORT_MID_WIDTH);
			Rect(PCB_SUPPORT_MID_WIDTH, PCB_SUPPORT_HEIGHT-PCB_SUPPORT_MID_WIDTH/2, ANCHOR_CT);
		}
		polygon([
			[-PCB_SUPPORT_BOTTOM_WIDTH/2,-PCB_SUPPORT_HEIGHT-EXTRA], 
			[PCB_SUPPORT_BOTTOM_WIDTH/2,-PCB_SUPPORT_HEIGHT-EXTRA], 
			[PCB_SUPPORT_MID_WIDTH/2,PCB_SUPPORT_MID_HEIGHT-PCB_SUPPORT_HEIGHT],
			[-PCB_SUPPORT_MID_WIDTH/2,PCB_SUPPORT_MID_HEIGHT-PCB_SUPPORT_HEIGHT]]);
	}
}

module Endhook()
{
	MINOR_HEIGHT = BODY_THICKNESS + PCB_SUPPORT_HEIGHT;
	pn_pos() {
		Rect(ENDHOOK_WIDTH, ENDHOOK_WIDTH+ENDHOOK_CLEARANCE, ANCHOR_LB, extraY=MINOR_HEIGHT);
		translate([0,ENDHOOK_CLEARANCE]) Rect(ENDHOOK_OVERHANG, ENDHOOK_WIDTH, ANCHOR_RB);
	}
	pn_pospos() {
		translate([-ENDHOOK_OVERHANG+ENDHOOK_PAD_WIDTH/2,ENDHOOK_CLEARANCE])
			Capsule(ENDHOOK_PAD_WIDTH, ENDHOOK_CLEARANCE-THICKNESS);
	}
}

module Undercut()
{
	pn_neg() translate([-UNDERCUT_WIDTH/2, -PCB_SUPPORT_HEIGHT]) RotZ(90) Capsule(UNDERCUT_DEPTH*2, UNDERCUT_WIDTH);
}

module BenderBody()
{
	LARGE = 1000;

	pn_pos() intersection()
	{
		difference() {
			circle(BODY_RADIUS);
			circle(BODY_RADIUS-BODY_THICKNESS);
		}
		polygon([
			[0,0],
			[LARGE*tan(FINGERBOARD_PCB_ANGLE/2), LARGE],
			[-LARGE*tan(FINGERBOARD_PCB_ANGLE/2), LARGE]]);
	}

	OnArc(-19) PCBSupport();
	OnArc(0) PCBSupport();
	OnArc(19) PCBSupport();
	OnArc(FINGERBOARD_PCB_LENGTH/2) Endhook();
	OnArc(-FINGERBOARD_PCB_LENGTH/2 + BODY_THICKNESS/2) translate([0,-PCB_SUPPORT_HEIGHT-BODY_THICKNESS/2])
		InterconnectBoltHole();
	OnArc(FINGERBOARD_PCB_LENGTH/2 + ENDHOOK_WIDTH/2) translate([0,ENDHOOK_WIDTH])
		InterconnectBoltHole();

	OnArc(FINGERBOARD_PCB_LENGTH/2) Undercut();
}

Dekerf() pn_top() BenderBody();